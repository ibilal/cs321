// This is supporting software for CS321/CS322 Compilers and Language Design.
// Copyright (c) Portland State University.
//---------------------------------------------------------------------------
// For CS321 F'15 (J. Li).
//

// A raw grammar for the miniJava language. (JavaCC Specification)
//

// options { DEBUG_PARSER=true; }  /* Show debugging info */

// Iman Bilal
// CS 321
// Homework 2 Parser


PARSER_BEGIN(LLGrammar)
import java.io.*;

public class LLGrammar {
  public static void main(String [] args) {
    if (args.length < 1) {
      System.out.println("Need a file name as command-line argument.");
      return;
    } 
    try {
      FileReader input = new FileReader(args[0]);
      new LLGrammar(input).Program();
      input.close();
    } catch (TokenMgrError e) {
      System.err.println(e);
    } catch (Exception e) {
      System.err.println(e);
    }
  }
}
PARSER_END(LLGrammar)

//
// LEXER SECTION ---------------------------------------------------------------
//
// A simplified version. Error detections are not included.
// 
SKIP:  /* Whitespace */
{ 
  " " | "\t" | "\r" | "\n" 
}

SKIP:  /* Comments -- single-line form only */
{
  <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")> 
}

TOKEN:  /* Keywords */
{
  "class" | "extends" | "static" | "public" | "main" | "void" | "boolean"
| "int" | "double" | "String" | "true" | "false" | "new" | "this" | "if"
| "else" | "while" | "return" | "System" | "out" | "println"
}

TOKEN:  /* Literals and IDs */
{
  <#DIGIT:  ["0"-"9"]>
| <#LETTER: ["A"-"Z"]|["a"-"z"]>
| <INTLIT:  "0"|["1"-"9"](<DIGIT>)*> 	// decimal form only
| <DBLLIT:  (<DIGIT>)+"."(<DIGIT>)*|(<DIGIT>)*"."(<DIGIT>)+> 
| <STRLIT:  ("\"" (~["\"","\r","\n"])* "\"")>
| <ID:      <LETTER> (<LETTER>|<DIGIT>)*>
}

TOKEN:  /* Operators and delimiters */
{
  "+" | "-" | "*" | "/" | "&&" | "||" | "!" | "==" | "!=" | "<" | "<=" 
| ">" | ">=" |  "=" | ";" | "," | "." | "(" | ")" | "[" | "]" | "{" | "}"
}

//
// PARSER SECTION ---------------------------------------------------------------
//

// Program -> {ClassDecl}
//
void Program(): {}
{
  (ClassDecl())* <EOF>
}

// ClassDecl -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
//
void ClassDecl(): {}
{
  "class" <ID> ["extends" <ID>] "{" (LOOKAHEAD(2)VarDecl())* (MethodDecl())* "}"
}

// MethodDecl -> "public" (ExtType <ID> "(" [Param {"," Param}] ")" "{" {VarDecl} {Stmt} "}" 
//                          | 
//                        "static" "void" "main" "(" "String" "[" "]" <ID> ")" "{" {VarDecl} {Stmt} "}")
//
void MethodDecl(): {} 
{
  "public" (ExtType() <ID> "(" [Param() ("," Param())*] ")" "{" (LOOKAHEAD(2)(VarDecl()))* (Stmt())* "}" | 
            "static" "void" "main" "(" "String" "[" "]" <ID> ")" "{" (LOOKAHEAD(2) (VarDecl()))* (Stmt())* "}")
}

// Param -> Type <ID> 
//
void Param(): {} 
{
  Type() <ID> 
}

// VarDecl -> Type <ID> ["=" InitExpr] ";"
//  
void VarDecl(): {}
{
  Type() <ID> ["=" InitExpr()] ";"
}

// ExtType -> Type | "void"
//
void ExtType(): {}
{
  Type() | "void"
}

// Type -> BasicType ["[" "]" | <ID>
//
void Type(): {}
{
  BasicType() ["[" "]"] | <ID>
}

// BasicType -> "int" | "double" | "boolean"
//
void BasicType(): {}
{
  "int" | "double" | "boolean"
}

// Stmt -> "{" {Stmt} "}"
//      |  ExtId ["(" [Args] ")" ";" | "[" Expr() "]"] ["=" InitExpr()] ";"
//      |  "if" "(" Expr ")" Stmt ["else" Stmt]
//      |  "while" "(" Expr ")" Stmt
//      |  "System" "." "out" "." "println" "(" [PrArg] ")" ";"
//      |  "return" [Expr] ";"
//
void Stmt(): {}
{
"{" (Stmt())* "}"
| ExtId() ["(" [Args()] ")" | "[" Expr() "]"] ["=" InitExpr()] ";"
| "if" "(" Expr() ")" Stmt() [LOOKAHEAD(2)"else" Stmt()]
| "while" "(" Expr() ")" Stmt()
| "System" "." "out" "." "println" "(" [ PrArg() ] ")" ";"  
| "return" [Expr()] ";"
}

// Args -> Expr {"," Expr}
//
void Args(): {}
{
  Expr() ("," Expr())*
}

// PrArg -> Expr | <STRLIT>
//
void PrArg(): {}
{
  Expr() | <STRLIT>
}

// InitExpr -> "new" InitExprExtend
//          |  Expr 
//
void InitExpr(): {}
{
  "new" [BasicType() "[" <INTLIT> "]" | <ID> "(" ")"] | Expr()
}

// Expr -> BinOpOR
//
void Expr():{}
{
    BinOpOR()
}

// BinOpOR -> BinOpAND {"||" BinOPAND}
//
void BinOpOR():{}
{
    BinOpAND() ("||" BinOpAND())*
}

// BinOpAND -> BinOpCOMP {"||" BinOPCOMP}
//
void BinOpAND():{}
{
    BinOpCOMP() ("&&" BinOpCOMP())*
}

// BinOpCOMP -> BinOpAddSub {"||" BinOpAddSub}
//
void BinOpCOMP():{}
{
    BinOpAddSub() ((">="|">"|"<="|"<"|"!="|"==") BinOpAddSub())*
}

// BinOpAddSub -> BinOpMulDiv {"||" BinOpMulDiv}
//
void BinOpAddSub():{}
{
    BinOpMulDiv() (("+"|"-") BinOpMulDiv())*
}

// BinOpMulDiv -> BinOpMulDiv {"||" BinOpMulDiv}
//
void BinOpMulDiv():{}
{
    UnOp() (("*"|"/") UnOp())*
}

// UnOp -> {("!"|"-") MethCall}
//
void UnOp():{}
{
    ("-"|"!")* MethCall()
}

// MethCall -> ExtId() ["(" [Args()] ")" | "[" Expr() "]"]
//                | Paren()
//                | Literal()
//
void MethCall():{}
{
    ExtId() ["(" [Args()] ")" | "[" Expr() "]"]
    | Paren()
    | Literal()
}

// Paren -> "(" Expr ")"
//
void Paren():{}
{
    "(" Expr() ")"
}

// ExtId -> ["this" "."] <ID> {"." <ID>} 
//
void ExtId(): {}
{
  ["this" "."] <ID> ("." <ID>)*
}

// Literal -> <INTLIT> | <DBLLIT> | "true" | "false"
//
void Literal(): {}
{
  <INTLIT> | <DBLLIT> | "true" | "false"
}
